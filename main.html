<!DOCTYPE html>
<html>

<head>
	<script type="text/javascript" src="gl-matrix.js"></script>
	
<script id="particle-shader-fs" type="x-shader/x-fragment">
    precision mediump float;
	
	varying vec4 r1;
	varying vec4 r2;
	varying vec4 r3;
	varying vec4 r4;
	varying vec4 r5;
	varying vec4 r6;
	varying vec4 r7;
	varying vec4 r8a;
	varying vec4 r9;
	
    void main(void) {
		gl_FragColor = vec4(0.0, 0.0, 0.0,0.0);
		
		if(gl_PointCoord.t < 0.35) {
			if(gl_PointCoord.s < 0.35)
				gl_FragColor = r1;
			else if(gl_PointCoord.s > 0.65)
				gl_FragColor = r3;
			else
				gl_FragColor = r2;
		}
		else if(gl_PointCoord.t > 0.65) {
			if(gl_PointCoord.s < 0.35)
				gl_FragColor = r7;
			else if(gl_PointCoord.s > 0.65)
				gl_FragColor = r9;
			else
				gl_FragColor = r8a;
		}
		else {
			if(gl_PointCoord.s < 0.35)
				gl_FragColor = r4;
			else if(gl_PointCoord.s > 0.65)
				gl_FragColor = r6;
			else
				gl_FragColor = r5;
		}
    }
</script>

<script id="particle-shader-vs" type="x-shader/x-vertex">
    attribute vec4 aVertexPosition;

    uniform mat4 uPMatrix;
	uniform mat4 uAttractors;
	uniform int uCallCount;
	
	uniform sampler2D uSampler;
	
	varying vec4 r1;
	varying vec4 r2;
	varying vec4 r3;
	varying vec4 r4;
	varying vec4 r5;
	varying vec4 r6;
	varying vec4 r7;
	varying vec4 r8a;
	varying vec4 r9;
	
	vec4 EncodeFloatRGBA( float v ) {
		vec4 enc = vec4(1.0, 255.0, 65025.0, 16581375.0) * v;
		enc = fract(enc);
		enc -= enc.yzww * vec4(1.0/255.0,1.0/255.0,1.0/255.0,0.0);
		return enc;
	}
	float DecodeFloatRGBA( vec4 rgba ) {
		return dot( rgba, vec4(1.0, 1.0/255.0, 1.0/65025.0, 1.0/16581375.0) );
	}
	
    void main(void) {
		gl_PointSize = 3.0;	
		
		vec3 pos;
		vec3 vel;
		if(uCallCount == 0)
		{
			pos = vec3(aVertexPosition[0], aVertexPosition[1], aVertexPosition[2]);
			vel = vec3(0.0,0.0,0.0);
		}
		else
		{
			vec2 texture_pos = vec2(
				mod(aVertexPosition[3], 4095.0 / 3.0), 
				(aVertexPosition[3] - mod(aVertexPosition[3], 4095.0/ 3.0)) / (4095.0 / 3.0)
			);
			
			pos = vec3(
				DecodeFloatRGBA(texture2D(uSampler, (floor(vec2(texture_pos.x*3.0 + 0.0, texture_pos.y*3.0+2.0)) +0.5) / 4096.0)),
				DecodeFloatRGBA(texture2D(uSampler, (floor(vec2(texture_pos.x*3.0 + 1.0, texture_pos.y*3.0+2.0)) +0.5) / 4096.0)),
				DecodeFloatRGBA(texture2D(uSampler, (floor(vec2(texture_pos.x*3.0 + 2.0, texture_pos.y*3.0+2.0)) +0.5) / 4096.0))
			);
			vel = vec3(
				DecodeFloatRGBA(texture2D(uSampler, (floor(vec2(texture_pos.x*3.0 + 0.0, texture_pos.y*3.0+0.0)) +0.5) / 4096.0)),
				DecodeFloatRGBA(texture2D(uSampler, (floor(vec2(texture_pos.x*3.0 + 1.0, texture_pos.y*3.0+0.0)) +0.5) / 4096.0)),
				DecodeFloatRGBA(texture2D(uSampler, (floor(vec2(texture_pos.x*3.0 + 2.0, texture_pos.y*3.0+0.0)) +0.5) / 4096.0))
			);
			pos = (pos * 1000.0) - 500.0;
			vel = (vel * 1000.0) - 500.0;
		}

		vec3 acc = vec3(0.0,0.0,0.0);
		for(int an = 0; an < 4; an++)
		{
			vec3 at_pos = vec3(uAttractors[an][0], uAttractors[an][1], uAttractors[an][2]);
			float strenght = uAttractors[an][3];
			
			vec3 diff = at_pos - pos;
			float dist = length(diff);
				
			acc = acc + (diff * strenght ) / ( dist * dist);
		}
		vel = vel + acc;
		pos = pos + vel;
		
		
		vec3 packed_pos = (pos + 500.0) / 1000.0;
		vec3 packed_vel = (vel + 500.0) / 1000.0;
		
		r1 = EncodeFloatRGBA(packed_pos.x);
		r2 = EncodeFloatRGBA(packed_pos.y);
		r3 = EncodeFloatRGBA(packed_pos.z);
		
		r4 = vec4(1.0, 1.0, 1.0, 1.0);
		r5 = vec4(1.0, 1.0, 1.0, 1.0);
		r6 = vec4(1.0, 1.0, 1.0, 1.0);
		
		r7 = EncodeFloatRGBA(packed_vel.x);
		r8a = EncodeFloatRGBA(packed_vel.y);
		r9 = EncodeFloatRGBA(packed_vel.z);
				
		
		
		vec2 return_pos = vec2(
			mod(aVertexPosition[3], 4095.0 / 3.0), 
			(aVertexPosition[3] - mod(aVertexPosition[3], 4095.0/ 3.0)) / (4095.0 / 3.0)
		);
        gl_Position = uPMatrix * vec4(1.5 + 3.0 * return_pos.x , 1.5 + 3.0 * return_pos.y, 0.0, 1.0);
    }
</script>

<script id="render-shader-fs" type="x-shader/x-fragment">
    precision mediump float;
	
    void main(void) {
		gl_FragColor = vec4(1.0, 0.0, 1.0, 1.0);
    }
</script>

<script id="render-shader-vs" type="x-shader/x-vertex">
    attribute vec4 aVertexPosition;

    uniform mat4 uPMatrix;
	uniform mat4 uMVMatrix;
	uniform sampler2D uSampler;
		
	vec4 EncodeFloatRGBA( float v ) {
		vec4 enc = vec4(1.0, 255.0, 65025.0, 16581375.0) * v;
		enc = fract(enc);
		enc -= enc.yzww * vec4(1.0/255.0,1.0/255.0,1.0/255.0,0.0);
		return enc;
	}
	float DecodeFloatRGBA( vec4 rgba ) {
		return dot( rgba, vec4(1.0, 1.0/255.0, 1.0/65025.0, 1.0/16581375.0) );
	}
	
    void main(void) {
		gl_PointSize = 1.0;	
		
		vec2 texture_pos = vec2(
			floor(mod(aVertexPosition[3], 4095.0 / 3.0)), 
			floor((aVertexPosition[3] - mod(aVertexPosition[3], 4095.0/ 3.0)) / (4095.0 / 3.0))
		);
		
		vec3 pos = vec3(
			DecodeFloatRGBA(texture2D(uSampler, (floor(vec2(texture_pos.x*3.0 + 0.0, texture_pos.y*3.0+2.0)) +0.5) / 4096.0)),
			DecodeFloatRGBA(texture2D(uSampler, (floor(vec2(texture_pos.x*3.0 + 1.0, texture_pos.y*3.0+2.0)) +0.5) / 4096.0)),
			DecodeFloatRGBA(texture2D(uSampler, (floor(vec2(texture_pos.x*3.0 + 2.0, texture_pos.y*3.0+2.0)) +0.5) / 4096.0))
		);
		pos = (pos * 1000.0) - 500.0;
		//pos.z = 0.0;

        gl_Position = uPMatrix * uMVMatrix * vec4(pos, 1.0);
    }
</script>
	
	<script type="text/javascript" src="shader.core.js"></script>
	<script type="text/javascript" src="shader.particles.js"></script>
	<script type="text/javascript" src="shader.render.js"></script>
	<script type="text/javascript" src="core.js"></script>
</head>

<body onload="main();">
    <canvas id="canvas_main" style="border: none;" width="1280" height="720"></canvas>
</body>

</html>